export dictToNamedTuple
export dropFields
export foldlUnrolled
export getCombinedNamedTuple
export getNamedTupleFromTable
export makeNamedTuple
export nonUnique
export removeEmptyTupleFields
export setTupleField
export setTupleSubfield
export tabularizeList
export tcPrint


"""
    collectColorForTypes(d; _color = true)

Collect colors for all types from nested namedtuples.

# Arguments
- `d`: The input data structure
- `_color`: Whether to use colors (default: true)

# Returns
- A dictionary mapping types to color codes
"""
function collectColorForTypes(d; _color=true)
    all_types = []
    all_types = getTypes!(d, all_types)
    c_types = Dict{DataType,Int}()
    _default_colors = [v for (k,v) in StyledStrings.ANSI_4BIT_COLORS]
    for (i,t) ∈ enumerate(all_types)
        if _color == true
            c = i<17 ? _default_colors[i] : rand(16:255)
        else
            c = 0
        end
        c_types[t] = c
    end
    return c_types
end


"""
    dictToNamedTuple(d::AbstractDict)

Convert a nested dictionary to a NamedTuple.

# Arguments
- `d::AbstractDict`: The input dictionary to convert

# Returns
- A NamedTuple with the same structure as the input dictionary
"""
function dictToNamedTuple(d::AbstractDict)
    for k ∈ keys(d)
        if d[k] isa Array{Any,1}
            d[k] = [v for v ∈ d[k]]
        elseif d[k] isa DataStructures.OrderedDict
            d[k] = dictToNamedTuple(d[k])
        end
    end
    dTuple = NamedTuple{Tuple(Symbol.(keys(d)))}(values(d))
    return dTuple
end


"""
    foldlUnrolled(f, x::Tuple{Vararg{Any, N}}; init)

Generate an unrolled expression to run a function for each element of a tuple to avoid complexity of for loops 
for compiler.

# Arguments
- `f`: The function to apply
- `x`: The tuple to iterate through
- `init`: Initial value for the fold operation

# Returns
- The result of applying the function to each element
"""
@generated function foldlUnrolled(f, x::Tuple{Vararg{Any,N}}; init) where {N}
    exes = Any[:(init = f(init, x[$i])) for i ∈ 1:N]
    return Expr(:block, exes...)
end


"""
    dropFields(namedtuple::NamedTuple, names::Tuple{Vararg{Symbol}})

Remove specified fields from a NamedTuple.

# Arguments
- `namedtuple`: The input NamedTuple
- `names`: A tuple of field names to remove

# Returns
- A new NamedTuple with the specified fields removed
"""
function dropFields(namedtuple::NamedTuple, names::Tuple{Vararg{Symbol}}) 
    keepnames = Base.diff_names(Base._nt_names(namedtuple), names)
    return NamedTuple{keepnames}(namedtuple)
end

"""
    getCombinedNamedTuple(base_nt::NamedTuple, priority_nt::NamedTuple)

Combine property values from base and priority NamedTuples.

# Arguments
- `base_nt`: The base NamedTuple
- `priority_nt`: The priority NamedTuple whose values take precedence

# Returns
- A new NamedTuple combining values from both inputs
"""
function getCombinedNamedTuple(base_nt::NamedTuple, priority_nt::NamedTuple)
    combined_nt = (;)
    base_fields = propertynames(base_nt)
    var_fields = propertynames(priority_nt)
    all_fields = Tuple(unique([base_fields..., var_fields...]))
    for var_field ∈ all_fields
        field_value = nothing
        if hasproperty(base_nt, var_field)
            field_value = getfield(base_nt, var_field)
        else
            field_value = getfield(priority_nt, var_field)
        end
        if hasproperty(priority_nt, var_field)
            var_prop = getfield(priority_nt, var_field)
            if !isnothing(var_prop) && length(var_prop) > 0
                field_value = getfield(priority_nt, var_field)
            end
        end
        combined_nt = setTupleField(combined_nt,
            (var_field, field_value))
    end
    return combined_nt
end


"""
    getNamedTupleFromTable(tbl; replace_missing_values=false)

Convert a table to a NamedTuple.

# Arguments
- `tbl`: The input table
- `replace_missing_values`: Whether to replace missing values with empty strings

# Returns
- A NamedTuple representation of the table
"""
function getNamedTupleFromTable(tbl;replace_missing_values=false)
    a_nt = (;)
    for a_p in propertynames(tbl)
        t_p = getproperty(tbl, a_p)
        values_to_replace = t_p
        if replace_missing_values
            values_to_replace = [ismissing(t_p[i]) ? "" : t_p[i] for i in eachindex(t_p)]
        end
        values_to_replace = [values_to_replace...]
        a_nt = setTupleField(a_nt, (a_p, values_to_replace))
    end
    return a_nt
end


"""
    getTypes!(d, all_types)

Collect all types from nested namedtuples.

# Arguments
- `d`: The input data structure
- `all_types`: Array to store collected types

# Returns
- Array of unique types found in the data structure
"""
function getTypes!(d, all_types)
    for k ∈ keys(d)
        if d[k] isa NamedTuple
            push!(all_types, typeof(d[k]))
            getTypes!(d[k], all_types)
        else
            push!(all_types, typeof(d[k]))
        end
    end
    return unique(all_types)
end



"""
    makeNamedTuple(input_data, input_names)

Create a NamedTuple from input data and names.

# Arguments
- `input_data`: Vector of data values
- `input_names`: Vector of names for the fields

# Returns
- A NamedTuple with the specified names and values
"""
function makeNamedTuple(input_data, input_names)
    return (; Pair.(input_names, input_data)...)
end



export mergeNamedTuple

"""
Merges algorithm options by combining default options with user-provided options.

This function takes two option dictionaries and combines them, with user options
taking precedence over default options.

# Arguments
- `def_o`: Default options object (NamedTuple/Struct/Dictionary) containing baseline algorithm parameters
- `u_o`: User options object containing user-specified overrides

# Returns
- A merged object containing the combined algorithm options
"""
function mergeNamedTuple(def_o, u_o)
    c_o = deepcopy(def_o)
    for p in keys(u_o)

        c_o = mergeNamedTupleSetValue(c_o, p, getproperty(u_o, p))
    end
    return c_o
end

"""
    mergeNamedTupleSetValue(o, p, v)

Set a field in an options object.

# Arguments
- `o`: The options object (NamedTuple or mutable struct)
- `p`: The field name to update
- `v`: The new value to assign

# Variants:
1. **For `NamedTuple` options**:
   - Updates the field in an immutable `NamedTuple` by creating a new `NamedTuple` with the updated value.
   - Uses the `@set` macro for immutability handling.

2. **For mutable struct options (e.g., BayesOpt)**:
   - Directly updates the field in the mutable struct using `Base.setproperty!`.

# Returns:
- The updated options object with the specified field modified.

# Notes:
- This function is used internally by `mergeNamedTuple` to handle field updates in both mutable and immutable options objects.
- Ensures compatibility with different types of optimization algorithm configurations.

# Examples:
1. **Updating a `NamedTuple`**:
```julia
options = (max_iters = 100, tol = 1e-6)
updated_options = mergeNamedTupleSetValue(options, :tol, 1e-8)
```

2. **Updating a mutable struct**:
```julia
mutable struct BayesOptConfig
    max_iters::Int
    tol::Float64
end
config = BayesOptConfig(100, 1e-6)
updated_config = mergeNamedTupleSetValue(config, :tol, 1e-8)
```
"""
function mergeNamedTupleSetValue end

function mergeNamedTupleSetValue(o::NamedTuple, p, v)
    o = @set o[p] = v
    return o
end


function mergeNamedTupleSetValue(o, p, v)
    Base.setproperty!(o, p, v);
    return o
end




"""
    nonUnique(x::AbstractArray{T}) where T

Finds and returns a vector of duplicate elements in the input array.

# Arguments:
- `x`: The input array.

# Returns:
A vector of duplicate elements.
"""
function nonUnique(x::AbstractArray{T}) where {T}
    xs = sort(x)
    duplicatedvector = T[]
    for i ∈ eachindex(xs)[2:end]
        if (
            isequal(xs[i], xs[i-1]) &&
            (length(duplicatedvector) == 0 || !isequal(duplicatedvector[end], xs[i]))
        )
            push!(duplicatedvector, xs[i])
        end
    end
    return duplicatedvector
end



"""
    removeEmptyTupleFields(tpl::NamedTuple)

Remove all empty fields from a NamedTuple.

# Arguments
- `tpl`: The input NamedTuple

# Returns
- A new NamedTuple with empty fields removed
"""
function removeEmptyTupleFields(tpl::NamedTuple)
    indx = findall(x -> x != NamedTuple(), values(tpl))
    nkeys, nvals = tuple(collect(keys(tpl))[indx]...), values(tpl)[indx]
    return NamedTuple{nkeys}(nvals)
end


"""
    setTupleSubfield(tpl, fieldname, vals)

Set a subfield of a NamedTuple.

# Arguments
- `tpl`: The input NamedTuple
- `fieldname`: The name of the field to set
- `vals`: Tuple containing subfield name and value

# Returns
- A new NamedTuple with the updated subfield
"""
function setTupleSubfield(tpl::NamedTuple, fieldname::Symbol, vals::Tuple{Symbol, Any})
    if !hasproperty(tpl, fieldname)
        tpl = setTupleField(tpl, (fieldname, (;)))
    end
    return (; tpl..., fieldname => (; getfield(tpl, fieldname)..., first(vals) => last(vals)))
end


"""
    setTupleField(tpl, vals)

Set a field in a NamedTuple.

# Arguments
- `tpl`: The input NamedTuple
- `vals`: Tuple containing field name and value

# Returns
- A new NamedTuple with the updated field
"""
setTupleField(tpl::NamedTuple, vals::Tuple{Symbol, Any}) = (; tpl..., first(vals) => last(vals))



"""
    tabularizeList(_list)

Converts a list or tuple into a table using `TypedTables`.

# Arguments:
- `_list`: The input list or tuple.

# Returns:
A table representation of the input list.
"""
function tabularizeList(_list)
    table = Table((; name=[_list...]))
    return table
end

"""
    tcPrint(d; _color=true, _type=true, _value=true, t_op=true)

Print a formatted representation of a data structure with type annotations and colors.

# Arguments
- `d`: The object to print
- `_color`: Whether to use colors (default: true)
- `_type`: Whether to show types (default: false)
- `_value`: Whether to show values (default: true)
- `_tspace`: Starting tab space
- `space_pad`: Additional space padding

# Returns
- Nothing (prints to console)
"""
function tcPrint(d; _color=true, _type=false, _value=true, _tspace="", space_pad="")
    colors_types = collectColorForTypes(d; _color=_color)
    # aio = StyledStrings.AnnotatedIOBuffer()
    lc = nothing
    ttf = _tspace * space_pad
    for k ∈ sort(collect(keys(d)))
        if d[k] isa NamedTuple
            tp = " = (;"
            if length(d[k])>0
                printstyled(Crayon(; foreground=colors_types[typeof(d[k])]), "$(k)$(tp)\n")
            else
                printstyled(Crayon(; foreground=colors_types[typeof(d[k])]), "$(k)$(tp)")
            end
            tcPrint(d[k]; _color=_color, _type=_type, _value=_value, _tspace = ttf, space_pad="  ")
        else
            if _type == true
                tp = "::$(typeof(d[k]))"
                if tp == "::NT"
                    tp = "::Tuple"
                end
            else
                tp = ""
            end
            if typeof(d[k]) <: Float32
                to_print = "$(ttf) $(k) = $(d[k])f0$(tp),\n"
                if !_value
                    to_print = "$(ttf) $(k)$(tp),\n"
                end
                print(Crayon(; foreground=colors_types[typeof(d[k])]),
                    to_print)
            elseif typeof(d[k]) <: SVector
                to_print = "$(ttf) $(k) = SVector{$(length(d[k]))}($(d[k]))$(tp),\n"
                if !_value
                    to_print = "$(ttf) $(k)$(tp),\n"
                end
                print(Crayon(; foreground=colors_types[typeof(d[k])]),
                to_print)
            elseif typeof(d[k]) <: Matrix
                print(Crayon(; foreground=colors_types[typeof(d[k])]), "$(ttf) $(k) = [\n")
                tt_row = repeat(ttf[1], length(ttf) + 1)
                for _d ∈ eachrow(d[k])
                    d_str = nothing
                    if eltype(_d) == Float32
                        d_str = join(_d, "f0 ") * "f0"
                    else
                        d_str = join(_d, " ")
                    end
                    print(Crayon(; foreground=colors_types[typeof(d[k])]),
                        "$(tt_row) $(d_str);\n")
                end
                print(Crayon(; foreground=colors_types[typeof(d[k])]), "$(tt_row) ]$(tp),\n")
            else
                to_print = "$(ttf) $(k) = $(d[k])$(tp),"
                if !_value
                    to_print = "$(ttf) $(k)$(tp),"
                end
                print(Crayon(; foreground=colors_types[typeof(d[k])]),
                    to_print)
            end
            lc = colors_types[typeof(d[k])]
        end
        # end
        if _type == true
            _tspace = _tspace * " "
            print(Crayon(; foreground=lc), " $(ttf))::NamedTuple,\n")
        else
            if d[k] isa NamedTuple
                print(Crayon(; foreground=lc), "$(ttf)),\n")
            end
        end
    end
end

